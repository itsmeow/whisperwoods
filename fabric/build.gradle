apply from: "https://maven.itsmeow.dev/scripts/architectury-fabric.gradle"

configurations {
    shadowModImplementation
    modImplementation.extendsFrom shadowModImplementation
}

loom {
    accessWidenerPath = file("src/main/resources/whisperwoods.accesswidener")
}

repositories {
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        url = "https://maven.shedaniel.me/"
    }
    maven {
        url = "https://cursemaven.com/"
    }
}

dependencies {
    shadowModImplementation("dev.itsmeow.imdlib:imdlib-fabric:${rootProject.imdlib_version}") {
        transitive = false
    }
    modRuntimeOnly("com.terraformersmc:modmenu:4.0.4") {
        transitive = false
    }
    modRuntimeOnly ("curse.maven:cloth-config-348521:3871534") {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
    }
    runtimeOnly("me.shedaniel.cloth:basic-math:0.6.1")
}

shadowJar {
    configurations = [project.configurations.shadowCommon, project.configurations.shadowModImplementation]
    relocate 'dev.itsmeow.imdlib', "${rootProject.maven_group}.imdlib"
    classifier "dev-shadow"
}

remapJar {
    classifier "remap"
}

task fixJar(type: Jar) {
    dependsOn remapJar
    from remapJar.archiveFile.map { zipTree(it) }
    manifest {
        from {
            zipTree(remapJar.archiveFile).find {
                it.name == "MANIFEST.MF"
            }
        }
    }
    filesMatching("**/FabricMixinPlugin.java") {
        filter {
            line -> {
                line.replaceAll("dev.itsmeow.imdlib.mixin", "${rootProject.maven_group}.imdlib.mixin")
            }
        }
    }
    filesMatching("**/fabric.mod.json") {
        filter {
            line -> {
                line = line.replaceAll("dev.itsmeow.imdlib", "${rootProject.maven_group}.imdlib")
                line = line.replaceAll('mixin.imdlib.json', "mixin.imdlib-${rootProject.mod_id}.json\",\"mixin.imdlib-${rootProject.mod_id}-fabric.json")
            }
        }
    }
    rename("mixin.imdlib.json", "mixin.imdlib-${rootProject.mod_id}.json")
    rename("imdlib-common-refmap.json", "imdlib-${rootProject.mod_id}-common-refmap.json")
    filesMatching("mixin.imdlib.json") {
        filter {
            line ->
                {
                    line = line.replaceAll('dev.itsmeow.imdlib', "${rootProject.maven_group}.imdlib")
                    line.replaceAll('imdlib-common-refmap.json', "imdlib-${rootProject.mod_id}-common-refmap.json")
                }
        }
    }
    rename("mixin.imdlib-fabric.json", "mixin.imdlib-${rootProject.mod_id}-fabric.json")
    rename("imdlib-fabric-refmap.json", "imdlib-${rootProject.mod_id}-fabric-refmap.json")
    filesMatching("mixin.imdlib-fabric.json") {
        filter {
            line ->
                {
                    line = line.replaceAll('dev.itsmeow.imdlib.mixin', "${rootProject.maven_group}.imdlib.mixin")
                    line = line.replaceAll('dev.itsmeow.imdlib.FabricMixinPlugin', "${rootProject.maven_group}.imdlib.FabricMixinPlugin")
                    line.replaceAll('imdlib-fabric-refmap.json', "imdlib-${rootProject.mod_id}-fabric-refmap.json")
                }
        }
    }
    filesMatching("imdlib-common-refmap.json") {
        filter {
            line -> line.replaceAll('dev/itsmeow/imdlib/mixin/', "dev/itsmeow/${rootProject.mod_id}/imdlib/mixin/")
        }
    }
    filesMatching("imdlib-fabric-refmap.json") {
        filter {
            line -> line.replaceAll('dev/itsmeow/imdlib/mixin/', "dev/itsmeow/${rootProject.mod_id}/imdlib/mixin/")
        }
    }
    classifier "fabric"
}

sourcesJar.dependsOn fixJar